buildscript {

    ext {
        es_version = System.getProperty("es.version", "6.5.4")
    }
    // This isn't applying from repositories.gradle so repeating it here
    repositories {

        //TODO: modify the order of evaluation, once we have artifacts in mavenCentral.
        maven {
            name = "internal-snapshots"
            url = "s3://snapshots.opendistroforelasticsearch.amazon.com/maven"
            authentication {
                awsIm(AwsImAuthentication) // load from EC2 role or env var
            }
        }
        maven {
            name = "internal-releases"
            url = "s3://artifacts.opendistroforelasticsearch.amazon.com/maven"
            authentication {
                awsIm(AwsImAuthentication) // load from EC2 role or env var
            }
        }
        mavenCentral()
    }

    dependencies {
        classpath "org.openes.gradle:build-tools:${es_version}"
    }
}

plugins {
    id "java"
    id "com.moowork.node" version "1.2.0"
}

node {
    version = "10.15.0"
    download = true
}

allprojects {
    group = "com.amazon.opendistroforelasticsearch"
    version = "1.0"
    boolean snapshot = "true".equals(System.getProperty("build.snapshot", "true"))
    if (snapshot) {
        // we update the version property to reflect if we are building a snapshot or a release build
        // we write this back out below to load it in the Build.java which will be shown in rest main action
        // to indicate this being a snapshot build or a release build.
        version += "-SNAPSHOT"
    }
}


repositories {
    mavenCentral()
}

apply plugin: 'elasticsearch.esplugin'

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}
test {
    enabled = false
}
licenseHeaders.enabled = false

task npmRunBuild(type: NpmTask, dependsOn: 'npmInstall') {
    args = args = ['run', 'build']
}

task npmRunClean(type: NpmTask) {
    args = ['run', 'clean']
}

compileJava {
    dependsOn npmRunBuild
}

clean {
    dependsOn npmRunClean
}

// We are excluding plugin descriptor for PerfTop
// The info here could be random
esplugin {
    name 'perf-top'
    description 'PerfTop'
    classname 'com.amazon.opendistro.performanceanalyzer.PerformanceAnalyzerApp'
    version "1.0"
    publishArtifact true
}


bundlePlugin {
    exclude "perf-top*.jar"
    exclude "plugin-descriptor.properties"
    from ("build/") {
        include "perf-top-linux"
        include "perf-top-macos"
        include "perf-top-win.exe"
    }
    from ("dashboards/") {
        into "dashboards/"
        include "*.json"
    }
}

gradle.startParameter.excludedTaskNames += ["integTestCluster#wait", "integTestRunner"]
